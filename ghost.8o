#----------------------------#
#  ghost
#  author: jackie kircher
#----------------------------#


#-- cutscenes --#
  : title   # G - 7 wide
    0x3C 0x62 0xC0 0xC0 0xDE 0xD2 0xC2 0x66 0x3C 0x00 0x00
  : title1  # L - 6 wide
    0x00 0xC0 0x40 0x40 0x40 0x40 0x80 0x44 0x7C 0x00 0x00
  : title2  # I - 3 wide
    0xE0 0x40 0x40 0x60 0x40 0x80 0x40 0x40 0xE0 0x00 0x00
  : title3  # T - 7 wide
    0x00 0xBE 0xF2 0x16 0x10 0x10 0x10 0x10 0x10 0x30 0x38
  : title4  # C - 7 wide
    0x3C 0x62 0xC0 0xC0 0xC0 0xC0 0xC2 0x66 0x3C 0x00 0x00
  : title5  # H - 7 wide
    0x00 0x00 0x84 0xC2 0x44 0x44 0x7C 0x46 0x46 0x84 0x40

  : title6  # G - 7 wide
    0x00 0x00 0x3C 0x62 0xC0 0xC0 0xDE 0xD2 0xC2 0x66 0x3C
  : title7  # H - 7 wide
    0x00 0x84 0xC2 0x44 0x44 0x7C 0x46 0x46 0x84 0x40 0x00
  : title8  # O - 7 wide
    0x7C 0xC6 0xC2 0x82 0x86 0x84 0x84 0xCC 0x78 0x00 0x00
  : title9  # S - 7 wide
    0x00 0x00 0x7C 0xC4 0xC0 0x78 0x08 0xC2 0xA4 0x8C 0x78
  : title10 # T - 7 wide
    0xBE 0xF2 0x16 0x10 0x10 0x10 0x10 0x10 0x30 0x38 0x00

  : alphabet
    0x00 0x00 0x00 0x00 0x00 # space
    0xF0 0x90 0xF0 0x90 0x90 # A (1)
    0xE0 0xA0 0xF0 0x90 0xF0 # B (2)
    0xF0 0x80 0x80 0x80 0xF0 # C (3)
    0xE0 0xA0 0x90 0x90 0xF0 # D (4)
    0xF0 0x80 0xE0 0x80 0xF0 # E (5)
    0xF0 0x80 0xE0 0x80 0x80 # F (6)
    0xE0 0x80 0xB0 0x90 0xF0 # G (7)
    0x90 0x90 0xF0 0x90 0x90 # H (8)
    0xE0 0x40 0x40 0x40 0xE0 # I (9)
    0x60 0x20 0x20 0x20 0xE0 # J (10)
    0x90 0xA0 0xC0 0xA0 0x90 # K (11)
    0x80 0x80 0x80 0x80 0xF0 # L (12)
    0x90 0xF0 0xB0 0x90 0x90 # M (13)
    0x90 0xD0 0xD0 0xB0 0x90 # N (14)
    0xF0 0x90 0x90 0x90 0xF0 # O (15)
    0xF0 0x90 0xF0 0x80 0x80 # P (16)
    0xE0 0x90 0x90 0xB0 0x70 # Q (17)
    0xE0 0x90 0xF0 0xA0 0x90 # R (18)
    0xF0 0x80 0xF0 0x10 0xF0 # S (19)
    0xF0 0x40 0x40 0x40 0x40 # T (20)
    0x90 0x90 0x90 0x90 0xF0 # U (21)
    0x90 0x90 0xA0 0xC0 0x80 # V (22)
    0x90 0x90 0xD0 0xF0 0x90 # W (23)
    0x90 0xD0 0x60 0xB0 0x90 # X (24)
    0x90 0x90 0xE0 0x40 0x40 # Y (25)
    0xF0 0x30 0x60 0xC0 0xF0 # Z (26)
    0x00 0x00 0x00 0x00 0x40 # . (27)
    0x80 0x80 0x80 0x00 0x80 # ! (28)

  : happy-ghost
    0x00 0x00 0x7C 0x54 0x7C 0xFE 0x7C 0x54 0x00 0x00 0x38
  : happy-ghost-mid
    0x00 0x7C 0x54 0xFE 0x7C 0x7C 0x54 0x00 0x00 0x00 0x10
  : happy-ghost-top
    0x7C 0xD6 0x7C 0x7C 0x7C 0x54 0x00 0x00 0x00 0x00 0x10


#-- sprites --#

  # human
  : human
    0xE0 0xE0 0x40 0x40 0xE0 0xA0 0xA0

  # ghost
  : ghost
    0xF8 0xA8 0xF8 0xF8 0xF8 0xA8 0x00 0x00 0x00 0x20
  : ghostlo
    0x00 0xF8 0xA8 0xF8 0xF8 0xF8 0xA8 0x00 0x00 0x70
  : ghostright
    0xF8 0xE8 0xF8 0xF8 0xF8 0xA8 0x00 0x00 0x00 0x20
  : ghostrightlo
    0x00 0xF8 0xE8 0xF8 0xF8 0xF8 0xA8 0x00 0x00 0x70
  : ghostleft
    0xF8 0xB8 0xF8 0xF8 0xF8 0xA8 0x00 0x00 0x00 0x20
  : ghostleftlo
    0x00 0xF8 0xB8 0xF8 0xF8 0xF8 0xA8 0x00 0x00 0x70
  : ghostup
    0xF8 0xF8 0xF8 0xF8 0xF8 0xA8 0x00 0x00 0x00 0x20
  : ghostuplo
    0x00 0xF8 0xF8 0xF8 0xF8 0xF8 0xA8 0x00 0x00 0x70

  # graves
  : grave0
    0x00 0x60 0x60 0x60 0x00 0x00 0x00 0x00 0x00 0x00
  : grave1
    0x40 0xA0 0xA0 0xE0 0x00 0x00 0x00 0x00 0x00 0x00
  : grave2
    0x40 0xE0 0x40 0x40 0x00 0x00 0x00 0x00 0x00 0x00
  : grave3
    0x00 0x60 0xF0 0x90 0x00 0x00 0x00 0x00 0x00 0x00
  : graveopen
    0x00 0x60 0xF0 0x90 0x00 0x90 0x00 0x90 0x00 0x90
  : graveclosed
    0x00 0x60 0xF0 0x90 0x00 0xF0 0x60 0xF0 0x60 0xF0

  # borders
  : border-left
    0x40 0xC0 0x40
  : border-right
    0x02 0x03 0x02
  : border-top
    0x40 0xE0
  : border-bottom
    0xE0 0x40
  : gate-left
    0x90 0x10 0xD4
  : gate-right
    0x12 0x10 0x56


#-- setup --#

  # game state
  : buried-counter 0

  # cutscenes
  : happy-ghost-idle   0
  : happy-ghost-frame  0
  : happy-ghost-delta 11

  # map
  :alias map_xpos   v8
  :alias map_ypos   v9
  : tmp  0 0 0 0 0
  : map
    0 0 0 0 0 0 0 0
    0 0 0 0 0 0 0 0 # end row 1
    0 0 0 0 0 0 0 0
    0 0 0 0 0 0 0 0 # end row 2

  :const HTOP 10
  :const HMID  8
  :const HBOT  6

  # humans
  :alias humanx v6
  :alias humany v7
  : human-scared  0
  : human-map-pos 0 0

  :const UP 1
  :const DN 2
  :const LF 3
  :const RT 4

  # ghost
  :alias ghostx     va
  :alias ghosty     vb
  :alias ghostframe vc
  :alias ghostpos   vd

  :const HI  0
  :const LO 10


#-- ghost --#

: idletimer
  0

: make-ghost
  ghostx     := 12 # starting x position
  ghosty     :=  4 # starting y position
  ghostframe :=  0 # starting frame
  ghostpos   := HI # bounce direction
;

: draw-ghost
  i := ghost
  i += ghostframe
  sprite ghostx ghosty 10
;

# bounce the ghost up and down
: idle-ghost
  v1 := 10
  i  := idletimer
  load v0
  v0 += v1
  v2 := vf
  i  := idletimer
  save v0
  if v2 == 1 then bounce-ghost
;

: move-ghost
  :alias newx     v1
  :alias newy     v2
  :alias newframe v3

  newx := ghostx
  newy := ghosty

  :alias input v0
  input := 7
  if input key then newx += -1
  input := 9
  if input key then newx +=  1
  input := 5
  if input key then newy += -1
  input := 8
  if input key then newy +=  1

  # return early and don't change anything if no
  # direction keys are being pressed
  v0 := 0
  if newx != ghostx then v0 := 1
  if newy != ghosty then v0 := 1
  if v0 == 0 then return

  # boundary checking y
  # boundaries are only applied if on the edge of the map
  v0 := 1
  if map_ypos !=  0 then v0   := 0
  if newy     !=  1 then v0   := 0
  if v0       ==  1 then newy := 2
  v0 := 1
  if map_ypos != 16 then v0   := 0
  if newy     != 21 then v0   := 0
  if v0       ==  1 then newy := 20

  # boundary checking x
  # boundaries are only applied if on the edge of the map
  v0 := 1
  if map_xpos !=  0 then v0   := 0
  if newx     !=  0 then v0   := 0
  if v0       ==  1 then newx := 2
  v0 := 1
  if map_xpos !=  8 then v0   := 0
  if newx     != 58 then v0   := 0
  if v0       ==  1 then newx := 57

  # update frame offset so ghost turns
  newframe := 0
  if newx > ghostx then newframe := 20
  if newx < ghostx then newframe := 40
  if newy < ghosty then newframe := 60
  newframe += ghostpos

  # erase old ghost position
  i := ghost
  i += ghostframe
  sprite ghostx ghosty 10

  # draw new ghost position
  i := ghost
  i += newframe
  sprite newx newy 10

  # update ghost values
  # (do this here so there is minimal flicker)
  ghostx     := newx
  ghosty     := newy
  ghostframe := newframe
;

: bounce-ghost
  :alias pos   v1
  :alias frame v2

  pos   := ghostpos
  frame := ghostframe

  if pos == HI then ghostframe +=  10
  if pos == HI then ghostpos   :=  LO
  if pos == LO then ghostframe += -10
  if pos == LO then ghostpos   :=  HI

  # erase the ghost on the previous frame
  i := ghost
  i += frame
  sprite ghostx ghosty 10

  # draw the ghost in the new position
  i := ghost
  i += ghostframe
  sprite ghostx ghosty 10
;

: haunt
  # remove current ghost
  i := ghost
  i += ghostframe
  sprite ghostx ghosty 10

  # glitch the ghost for 6 frames
  :alias hauntoffset v1
  hauntoffset := 0
  v0 := 160
  i  := ghost
  i  += v0
  loop
    sync

    i += hauntoffset
    sprite ghostx ghosty 10

    hauntoffset += 10
    if hauntoffset != 150 then
  again

  clear
  draw-background
  draw-ghost

  # don't scare the human if they're not on this screen
  :alias h_map_xpos v0
  :alias h_map_ypos v1
  i := human-map-pos
  load v1
  if map_xpos != h_map_xpos then return
  if map_ypos != h_map_ypos then return

  # scare them!
  calculate-scare-direction
  :alias scared v5
  scared := v0
  save-human-scared

  draw-human
;

: calculate-scare-direction
  :alias xdir v0
  :alias ydir v1

  # calculate the total distance between the ghost and the
  # human horizontally and the direction
  :alias ghost_cx v2
  :alias human_cx v3
  :alias xmag     v4

  ghost_cx := ghostx
  ghost_cx += 3
  human_cx := humanx
  human_cx += 2

  xdir := 0
  # ghost and human are at the same x position
  if ghost_cx == human_cx then xdir := 0
  if ghost_cx == human_cx then xmag := 0
  # ghost is the to the right of the human
  if ghost_cx >  human_cx then xdir := LF
  if ghost_cx >  human_cx then xmag := ghost_cx
  if ghost_cx >  human_cx then xmag -= human_cx
  # x is negative if ghost is left
  if ghost_cx <  human_cx then xdir := RT
  if ghost_cx <  human_cx then xmag := human_cx
  if ghost_cx <  human_cx then xmag -= ghost_cx


  # calculate the total distance between the ghost and the
  # human horizontally and the direction
  :alias ghost_cy v2
  :alias human_cy v3
  :alias ymag     v5

  ghost_cy := ghosty
  ghost_cy += 4
  human_cy := humany
  human_cy += 3

  ydir := 0 # direction to move vertically
  if ghost_cy == human_cy then ydir := 0
  if ghost_cy == human_cy then ymag := 0
  # y is positive if ghost is below
  if ghost_cy >  human_cy then ydir := UP
  if ghost_cy >  human_cy then ymag := ghost_cy
  if ghost_cy >  human_cy then ymag -= human_cy
  # y is negative if ghost is above
  if ghost_cy <  human_cy then ydir := DN
  if ghost_cy <  human_cy then ymag := human_cy
  if ghost_cy <  human_cy then ymag -= ghost_cy

  :alias scareto v2
  scareto := 0
  if ymag >= xmag then scareto := ydir
  if ymag <  xmag then scareto := xdir

  # if both xmag and ymag are 0, then the ghost is on top
  # of the human and it should jsut run in a random direction
  v0 := scareto
  if v0 == 0 then scareto := random 0b11
  if v0 == 0 then scareto += 1

  # return scareto direction in v0
  v0 := scareto
;


#-- human --#

: make-human
  humanx := 31
  humany := 14

  v0 := 0
  i  := human-scared
  save v0

  v0 := 0
  v1 := 0
  i  := human-map-pos
  save v1
;

# saves the value in 'scared' to human-scared
# -> an alias for scared must be set
: save-human-scared
  ve := v0
  v0 := scared
  i  := human-scared
  save v0

  v0 := ve
;

# loads the value for human-scared into 'scared'
# -> an alias for scared must be set
: load-human-scared
  ve := v0
  i  := human-scared
  load v0

  scared := v0
  v0     := ve
;

: draw-human
  # don't draw human if they aren't on the current screen
  i := human-map-pos
  load v1
  if map_xpos != v0 then return
  if map_ypos != v1 then return

  i := human
  sprite humanx humany 7

  :alias scared v5
  load-human-scared
  if scared != 0 then run-human-run
;

: run-human-run
  :alias human_nx v1
  :alias human_ny v2
  :alias scared   v5
  load-human-scared

  loop
    sync

    move-ghost
    idle-ghost

    human_nx := humanx
    human_ny := humany
    if scared == UP then human_ny += -1
    if scared == DN then human_ny +=  1
    if scared == LF then human_nx += -1
    if scared == RT then human_nx +=  1

    i := human
    sprite humanx   humany   7 # erase current human
    sprite human_nx human_ny 7 # draw new position

    v4 := vf
    if v4 == 0 then humanx := human_nx
    if v4 == 0 then humany := human_ny
    if v4 == 1 then check-grave-collision
               if v0 == 10 then return
    if v4 == 1 then human-turn

    # check to see if they're at the edge of the screen
    v0 := 0
    if humanx ==  0 then v0 := 1
    if humanx == 61 then v0 := 1
    if humany ==  0 then v0 := 1
    if humany == 25 then v0 := 1
    if v0 == 0 then
  again

  # "run off-screen"
  sprite humanx humany 7

  # update global map coordinates
  i := human-map-pos
  load v1
  if humanx ==  0 then v0 +=  -8
  if humanx == 61 then v0 +=   8
  if humany ==  0 then v1 += -16
  if humany == 25 then v1 +=  16
  i := human-map-pos
  save v1

  # set the new screen position
  v0 := random 0b110000
  v0 += 16
  if v0 == 64 then v0 := 32 # don't spawn to the far left
  humanx := v0

  v0 := random 0b1111
  v0 += 4
  humany := v0

  # calm down
  scared := 0
  save-human-scared
;

: human-turn
  :alias human_nx v1
  :alias human_ny v2

  v3 := random 0b1
  v3 += 3          # left or right
  v4 := random 0b1
  v4 += 1          # up or down

  :alias scared v5
  load-human-scared
  v0 := scared
  if v0 == UP then scared := v3
  if v0 == LF then scared := v4
  if v0 == DN then scared := v3
  if v0 == RT then scared := v4
  save-human-scared

  i := human
  sprite human_nx human_ny 7 # erase collision
  sprite humanx   humany   7 # draw in old position
;

: reset-human
  make-human
  clear
  draw-background
  draw-ghost
  draw-human
;


#-- background --#

# graves
: grave-table
  i := grave0      return
  i := grave1      return
  i := grave2      return
  i := grave3      return
  i := graveopen   return
  i := graveclosed return
;

: select-grave
  jump0 grave-table
;

: draw-grave
  :alias gravex v2
  :alias gravey v3

  i := map
  i += v1
  i += map_xpos
  i += map_ypos
  load v0

  # shift left because grave-table lines are 4 bytes each
  v0 <<= v0
  v0 <<= v0
  select-grave
  sprite gravex gravey 10
;

: make-background
  :alias j       v1
  :alias k       v2
  :alias openpos v3

  j := 0
  loop
    # don't make an opne grave on the first map screen
    if j != 0 then openpos := random 0b111
    if j == 0 then openpos := -1

    # inner loop is to create each screen
    # each screen (except for the generation screen) should
    # have exactly one open grave
    k := 0
    loop
      # choose a random grave 0-3
      v0 := random 0b11
      if k == openpos then v0 := 4

      i  := map
      i  += j
      i  += k
      save v0

      k += 1
      if k != 8 then
    again

    j += 8
    if j != 32 then
  again
;

: draw-background
  :alias j      v1
  :alias gravex v2
  :alias gravey v3

  gravey := HTOP
  if map_ypos == 16 then gravey := HBOT
  gravex := 7

  if map_xpos ==  0 then draw-border-left
  if map_xpos ==  8 then draw-border-right
  if map_ypos ==  0 then draw-border-top
  if map_ypos == 16 then draw-border-bottom

  j := 0
  loop
    draw-grave

    gravex += 16
    if j == 3 then gravey += 11

    j +=  1
    if j != 8 then
  again
;

# borders
: draw-border-left
  v4 :=  1
  v5 := 33
  if map_ypos == 16 then v4 :=  0
  if map_ypos == 16 then v5 := 32

  i  := border-left
  v1 := 0
  loop
    sprite v1 v4 3

    v4 += 4
    if v4 != v5 then
  again
;

: draw-border-right
  v4 :=  1
  v5 := 33
  if map_ypos == 16 then v4 :=  0
  if map_ypos == 16 then v5 := 32

  i  := border-right
  v1 := 56
  loop
    sprite v1 v4 3

    v4 += 4
    if v4 != v5 then
  again
;

: draw-border-top
  v4 :=  1
  v5 := 65
  if map_xpos == 8 then v4 :=  0
  if map_xpos == 8 then v5 := 64

  i  := border-top
  v1 := 0
  loop
    sprite v4 v1 2

    v4 += 4
    if v4 != v5 then
  again

  if map_xpos == 0 then draw-gate
;

: draw-border-bottom
  v4 :=  1
  v5 := 65
  if map_xpos == 8 then v4 :=  0
  if map_xpos == 8 then v5 := 64

  i  := border-bottom
  v1 := 30
  loop
    sprite v4 v1 2

    v4 += 4
    if v4 != v5 then
  again
;

: draw-gate
  v0 := 25
  v1 := 0

  i := gate-left
  sprite v0 v1 3
  v0 += 8
  i := gate-right
  sprite v0 v1 3
;

: new-screen-right
  map_xpos   +=  8
  ghostx     :=  0
  ghostframe := 20
  ghostpos   := HI
  clear
  draw-background
  draw-human
  draw-ghost
;

: new-screen-left
  map_xpos   += -8
  ghostx     := 58
  ghostframe := 40
  ghostpos   := HI
  clear
  draw-background
  draw-human
  draw-ghost
;

: new-screen-up
  map_ypos   += -16
  ghosty     :=  22
  ghostframe :=  60
  ghostpos   :=  HI
  clear
  draw-background
  draw-human
  draw-ghost
;

: new-screen-down
  map_ypos   += 16
  ghosty     :=  0
  ghostframe :=  0
  ghostpos   := HI
  clear
  draw-background
  draw-human
  draw-ghost
;

: check-grave-collision
  :alias human_nx v1
  :alias human_ny v2
  :alias gravepos v3

  if human_nx ==  1 then jump no-collision # left border
  if human_nx == 62 then jump no-collision # right border
  if human_ny ==  1 then jump no-collision # top border
  if human_ny ==  2 then jump no-collision # top gate
  # we cannot check lower y border collision because the
  # person could have hit an open grave running horizontally

  # check for x-collision with graves
  gravepos := -1

  # grave 0 (x = 7)
  v0 := 1
  if human_nx <  5 then v0 := 0
  if human_nx > 10 then v0 := 0
  if v0 == 1 then gravepos := 0

  # grave 1 (x = 23)
  v0 := 1
  if human_nx < 21 then v0 := 0
  if human_nx > 26 then v0 := 0
  if v0 == 1 then gravepos := 1

  # grave 2 (x = 39)
  v0 := 1
  if human_nx < 37 then v0 := 0
  if human_nx > 42 then v0 := 0
  if v0 == 1 then gravepos := 2

  # grave 3 (x = 55)
  v0 := 1
  if human_nx < 53 then v0 := 0
  if human_nx > 58 then v0 := 0
  if v0 == 1 then gravepos := 3

  # no horizontal collision with any grave
  if gravepos == -1 then jump no-collision

  # check for y-pos to determine row
  v0 := HTOP
  if map_ypos == 16 then v0 := HBOT
  v0 += 5
  if human_ny >  v0 then gravepos += 4

  # update gravepos to be absolute for the entre map
  gravepos += map_xpos
  gravepos += map_ypos

  i  := map
  i  += gravepos
  load v0

  if v0 ==  4 then bury-human
  if v0 == 10 then return

  # use v0 as a return flag
  # -> 0 means the human did not hit an open grave
  : no-collision
    v0 := 0
;

: bury-human
  :alias gravepos v3

  v0 := 5
  i  := map
  i  += gravepos
  save v0

  # increment the buried counter
  i  := buried-counter
  load v0
  v0 += 1
  i  := buried-counter
  save v0

  clear
  reset-human

  # use v0 as a return flag for check-grave-collision
  # -> 10 means the human hit an open grave
  v0 := 10
;


#-- cutscenes --#

: play-beginning
  clear

  draw-title

  loop
    v0 := 6
    if v0 -key then
  again

  sync # call sync to allow time to release buttons

  draw-prologue-page-one
  loop
    v0 := 6
    if v0 -key then
  again

  sync

  draw-prologue-page-two
  loop
    v0 := 6
    if v0 -key then
  again

  sync

  clear
;

: draw-title
  v3 := 11

  v1 :=  4
  v2 :=  4
  i  := title
  sprite v1 v2 11

  v1 +=  8
  i  += v3
  sprite v1 v2 11

  v1 +=  7
  i  += v3
  sprite v1 v2 11

  v1 +=  4
  i  += v3
  sprite v1 v2 11

  v1 +=  8
  i  += v3
  sprite v1 v2 11

  v1 +=  8
  i  += v3
  sprite v1 v2 11

  v1 := 16
  v2 := 16
  i  += v3
  sprite v1 v2 11

  v1 +=  8
  i  += v3
  sprite v1 v2 11

  v1 +=  8
  i  += v3
  sprite v1 v2 11

  v1 +=  8
  i  += v3
  sprite v1 v2 11

  v1 +=  7
  i  += v3
  sprite v1 v2 11
;

: draw-prologue-page-one
  # glitch ghost is the only ghost in the cemetary.

  clear
  v1 := 2
  v2 := 1

  v0 := 7
  print-letter
  v0 := 12
  print-letter
  v0 := 9
  print-letter
  v0 := 20
  print-letter
  v0 := 3
  print-letter
  v0 := 8
  print-letter

  v0 := 0
  print-letter
  v0 := 7
  print-letter
  v0 := 8
  print-letter
  v0 := 15
  print-letter
  v0 := 19
  print-letter
  v0 := 20
  print-letter

  newline

  v0 := 9
  print-letter
  v0 := 19
  print-letter

  v0 := 0
  print-letter
  v0 := 20
  print-letter
  v0 := 8
  print-letter
  v0 := 5
  print-letter

  v0 := 0
  print-letter
  v0 := 15
  print-letter
  v0 := 14
  print-letter
  v0 := 12
  print-letter
  v0 := 25
  print-letter

  newline

  v0 := 7
  print-letter
  v0 := 8
  print-letter
  v0 := 15
  print-letter
  v0 := 19
  print-letter
  v0 := 20
  print-letter

  v0 := 0
  print-letter
  v0 := 9
  print-letter
  v0 := 14
  print-letter

  v0 := 0
  print-letter
  v0 := 20
  print-letter
  v0 := 8
  print-letter
  v0 := 5
  print-letter

  newline

  v0 := 3
  print-letter
  v0 := 5
  print-letter
  v0 := 13
  print-letter
  v0 := 5
  print-letter
  v0 := 20
  print-letter
  v0 := 5
  print-letter
  v0 := 18
  print-letter
  v0 := 25
  print-letter
  v0 := 27
  print-letter
  v0 := 27
  print-letter
  v0 := 27
  print-letter
;

: draw-prologue-page-two
  # help them make some friends!

  clear
  v1 := 2
  v2 := 1

  v0 := 8
  print-letter
  v0 := 5
  print-letter
  v0 := 12
  print-letter
  v0 := 16
  print-letter

  v0 := 0
  print-letter
  v0 := 20
  print-letter
  v0 := 8
  print-letter
  v0 := 5
  print-letter
  v0 := 13
  print-letter

  newline

  v0 := 13
  print-letter
  v0 := 1
  print-letter
  v0 := 11
  print-letter
  v0 := 5
  print-letter

  v0 := 0
  print-letter
  v0 := 19
  print-letter
  v0 := 15
  print-letter
  v0 := 13
  print-letter
  v0 := 5
  print-letter

  newline

  v0 := 6
  print-letter
  v0 := 18
  print-letter
  v0 := 9
  print-letter
  v0 := 5
  print-letter
  v0 := 14
  print-letter
  v0 := 4
  print-letter
  v0 := 19
  print-letter
  v0 := 28
  print-letter
;

# take a number in v0 and print the corresponding letter
# at v1, v2
: print-letter
  if v0 == 0 then jump next-letter

  v4 := 0
  v5 := 0
  loop
    v5 += 5
    v4 += 1
    if v4 != v0 then
  again

  i := alphabet
  i += v5
  sprite v1 v2 5

  : next-letter
  v1 += 5
;

: newline
  v1 := 2
  v2 += 6
;

: play-ending
  clear

  draw-happy-ghosts

  # shift the map way offcenter so that there "isn't any"
  # collision detection for moving the ghost around
  map_xpos := 128
  map_ypos := 128

  ghostx := 30
  ghosty := 17
  draw-ghost

  v4 := 11
  loop

    v1 := 10
    i  := happy-ghost-idle
    load v0
    v0 += v1
    v2 := vf
    i  := happy-ghost-idle
    save v0
    if v2 == 1 then next-happy-ghost-frame

    move-ghost
    idle-ghost
  again
;

: draw-happy-ghosts
  i  := happy-ghost-frame
  load v0

  v1 := 14
  v2 :=  9
  i  := happy-ghost
  i  += v0
  sprite v1 v2 11

  v1 := 29
  v2 :=  4
  i  := happy-ghost
  i  += v0
  sprite v1 v2 11

  v1 := 44
  v2 :=  9
  i  := happy-ghost
  i  += v0
  sprite v1 v2 11
;

: next-happy-ghost-frame
  draw-happy-ghosts

  :alias delta v1
  :alias frame v2

  i := happy-ghost-delta
  load v0
  delta := v0

  i := happy-ghost-frame
  load v0
  frame := v0

  v2 += delta
  if v2 == 22 then delta := -11
  if v2 ==  0 then delta :=  11

  i  := happy-ghost-delta
  v0 := delta
  save v0

  i  := happy-ghost-frame
  v0 := frame
  save v0

  draw-happy-ghosts
;


#-- main --#
: sync
  loop
    vf := delay
    if vf != 0 then
  again

  # delay for up to 1/60th of a second
  # using the fixed-rate delay timer
  vf := 2
  delay := vf
;

: main

  play-beginning

  map_xpos := 0
  map_ypos := 0
  make-ghost
  draw-ghost

  make-human
  draw-human

  make-background
  draw-background

  loop
    sync

    i := buried-counter
    load v0
    if v0 == 3 then victory

    move-ghost

    v0 := 6
    if v0 key then haunt

    v0 := 1
    if v0 key then reset-human

    # wrap around effect
    if ghostx == 60 then new-screen-right
    if ghostx == -2 then new-screen-left
    if ghosty == -1 then new-screen-up
    if ghosty == 23 then new-screen-down

    idle-ghost
  again
;

: victory
  v1 := 0
  v2 := 32
  v0 := v1

  v3 := 0
  loop
    sync

    if v0 == v1 then buzzer := v2
    if v0 == v2 then buzzer := v1

    v4 := v0
    if v4 == v1 then v0 := v2
    if v4 == v2 then v0 := v1

    v3 += 1
    if v3 != 10 then
  again

  v0 := 0
  i  := buried-counter
  save v0

  play-ending
;

